*> PLDATE.

*>-------------------------------------------------------------------------
*>    Procedure to validate an informed date.

*>-------------------------------------------------------------------------
*>    IT will receive from MAIN-PROGRAM:

*>       GDTV-DATE-HEADING      ---  Heading containing date specification to be informed by the user
*>       GDTV-FIRST-YEAR-VALID  ---  First year-limit valid         
*>       GDTV-LAST-YEAR-VALID   ---  Last year-limit valid.
*>       GDTV-ACCEPT-EMPTY-DATE ---  "Y" or "N"

*>-------------------------------------------------------------------------
*>    It will return 
      
*>       GDTV-DATE (format CCYYMMDD)
*>-------------------------------------------------------------------------

GET-VALI-DATE-RETURN-GDTV-DATE.

   PERFORM GDTV-LOAD-DAYS-IN-THE-MONTH.
   PERFORM GDTV-GET-DATE-TO-VALIDATE. *> force first loop
   PERFORM GDTV-GET-DATE-TO-VALIDATE UNTIL GDTV-VALID-DATE-INFORMED.

   COMPUTE GDTV-DATE-TEMP-FOR-CALC = GDTV-DATE-MM-DD-CCYY * 10000.0001. *> It will be MMDDCCYYMMDD

   MOVE GDTV-DATE-TEMP-FOR-CALC TO GDTV-DATE. *> It will truncate with 8 digits from the left. Format will be CCYY-MM-DD

GDTV-GET-DATE-TO-VALIDATE.

   MOVE "Y" TO W-GDTV-VALID-DATE-INFORMED.
   
   IF GDTV-DATE-HEADING NOT EQUAL SPACES 
      DISPLAY GDTV-DATE-HEADING
   ELSE
      DISPLAY "ENTER A DATE (MMDDCCYY)".

   ACCEPT GDTV-DATE-MM-DD-CCYY. 

   IF GDTV-DATE-MM-DD-CCYY EQUAL ZEROS 

      PERFORM CHECK-IF-DATE-MUST-BE-INFORMED

   ELSE

      IF  GDTV-DATE-CCYY < GDTV-FIRST-YEAR-VALID 
                     OR
          GDTV-DATE-CCYY > GDTV-LAST-YEAR-VALID

         DISPLAY "YEAR INVALID ! (VALID RANGE " GDTV-FIRST-YEAR-VALID "-" GDTV-LAST-YEAR-VALID ")"     
         PERFORM GDTV-REFUSE-INFORMED-DATE

      ELSE 

         IF NOT GDTV-MONTH-VALID
            DISPLAY "MONTH INVALID (VALID RANGE 1-12)! "
            PERFORM GDTV-REFUSE-INFORMED-DATE

         ELSE

            PERFORM GDTV-CHECK-IF-LEAP-YEAR

            IF GDTV-DATE-DD > GDTV-TABLE-MONTH-NUMBER(GDTV-DATE-MM) 

               IF GDTV-DATE-MM = 2 
                  DISPLAY "ONLY **" GDTV-TABLE-MONTH-NUMBER(GDTV-DATE-MM) 
                          "** DAYS ARE ALLOWED FOR " GDTV-TABLE-MONTH-NAME(GDTV-DATE-MM) 
                          "/" GDTV-DATE-CCYY " !"   
                  PERFORM GDTV-REFUSE-INFORMED-DATE
               ELSE
                  DISPLAY "ONLY **" GDTV-TABLE-MONTH-NUMBER(GDTV-DATE-MM) 
                          "** DAYS ARE ALLOWED FOR " GDTV-TABLE-MONTH-NAME(GDTV-DATE-MM) " !"
                  PERFORM GDTV-REFUSE-INFORMED-DATE.

CHECK-IF-DATE-MUST-BE-INFORMED.

IF GDTV-ACCEPT-EMPTY-DATE = "N"
   DISPLAY "DATE MUST BE INFORMED !"
   PERFORM GDTV-REFUSE-INFORMED-DATE.

GDTV-REFUSE-INFORMED-DATE.
   DISPLAY "<ENTER> TO CONTINUE".
   ACCEPT GDTV-DUMMY.
   MOVE "N" TO W-GDTV-VALID-DATE-INFORMED.

GDTV-LOAD-DAYS-IN-THE-MONTH.

   MOVE 31 TO GDTV-TABLE-MONTH-NUMBER(1),
              GDTV-TABLE-MONTH-NUMBER(3),
              GDTV-TABLE-MONTH-NUMBER(5),
              GDTV-TABLE-MONTH-NUMBER(7),
              GDTV-TABLE-MONTH-NUMBER(8),
              GDTV-TABLE-MONTH-NUMBER(10),
              GDTV-TABLE-MONTH-NUMBER(12).

   MOVE 30 TO GDTV-TABLE-MONTH-NUMBER(4),
              GDTV-TABLE-MONTH-NUMBER(6),
              GDTV-TABLE-MONTH-NUMBER(9),
              GDTV-TABLE-MONTH-NUMBER(11).

   MOVE "JANUARY"   TO GDTV-TABLE-MONTH-NAME(1).
   MOVE "FEBRUARY"  TO GDTV-TABLE-MONTH-NAME(2).
   MOVE "MARCH"     TO GDTV-TABLE-MONTH-NAME(3).
   MOVE "APRIL"     TO GDTV-TABLE-MONTH-NAME(4).
   MOVE "MAY"       TO GDTV-TABLE-MONTH-NAME(5).
   MOVE "JUNE"      TO GDTV-TABLE-MONTH-NAME(6).
   MOVE "JULY"      TO GDTV-TABLE-MONTH-NAME(7).
   MOVE "AUGUST"    TO GDTV-TABLE-MONTH-NAME(8).
   MOVE "SEPTEMBER" TO GDTV-TABLE-MONTH-NAME(9).
   MOVE "OCTOBER"   TO GDTV-TABLE-MONTH-NAME(10).  
   MOVE "NOVEMBER"  TO GDTV-TABLE-MONTH-NAME(11).
   MOVE "DECEMBER"  TO GDTV-TABLE-MONTH-NAME(12).

GDTV-CHECK-IF-LEAP-YEAR.

   DIVIDE GDTV-DATE-CCYY BY 400 GIVING GDTV-LEAP-YEAR-DUMMY-QUO REMAINDER GDTV-LEAP-YEAR-REMAINDER. *> leap year

   IF GDTV-LEAP-YEAR-REMAINDER EQUAL ZEROS
      MOVE 29 TO GDTV-TABLE-MONTH-NUMBER(2)
   ELSE
      DIVIDE GDTV-DATE-CCYY BY 100 GIVING GDTV-LEAP-YEAR-DUMMY-QUO REMAINDER GDTV-LEAP-YEAR-REMAINDER *> not leap year

      IF GDTV-LEAP-YEAR-REMAINDER EQUAL ZEROS 
         MOVE 28 TO GDTV-TABLE-MONTH-NUMBER(2)
      ELSE
         DIVIDE GDTV-DATE-CCYY BY 4 GIVING GDTV-LEAP-YEAR-DUMMY-QUO REMAINDER GDTV-LEAP-YEAR-REMAINDER *> leap year

         IF GDTV-LEAP-YEAR-REMAINDER EQUAL ZEROS 
            MOVE 29 TO GDTV-TABLE-MONTH-NUMBER(2)
         ELSE
            MOVE 28 TO GDTV-TABLE-MONTH-NUMBER(2).




